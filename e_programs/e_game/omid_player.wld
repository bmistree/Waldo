omid_player

Endpoint OmidPlayer;
Endpoint OmidPlayerHelper;


Sequences {
  AddUser: OmidPlayer.doNothing -> OmidPlayerHelper.add_to_server;
  JoinWaiting: OmidPlayer.doNothing -> OmidPlayerHelper.addToWaiting;
  LeaveWaiting: OmidPlayer.doNothing -> OmidPlayerHelper.removePlayer;
  CheckGameInSession: OmidPlayer.doNothing -> OmidPlayerHelper.checkServer;
  SendWaitingMsg: OmidPlayer.doNothing -> OmidPlayerHelper.sendServer;
  DisplayWaiting: OmidPlayerHelper.doNothing -> OmidPlayer.displayWaitingMessage;
  UpdateGame: OmidPlayerHelper.setMap -> OmidPlayer.updateMap;
  AddToGame: OmidPlayer.doNothing -> OmidPlayerHelper.addPlayer;
  SendAnswer: OmidPlayer.doNothing -> OmidPlayerHelper.send;
}

Struct node_information {
    Text answer;
    Text node_num;
    Number x;
    Number y;
    TrueFalse found;
}

Struct arc_information {
    Text node1;
    Text node2;
}


Peered {
    Text username;
}

Sequence SendAnswer(Text node_num, Text answer) {
    Number score;
    OmidPlayer.doNothing{}
    OmidPlayerHelper.send{
        score = game_server.check_node(username, node_num, answer);
    }
}

Sequence AddUser() {
    OmidPlayer.doNothing{}
    OmidPlayerHelper.add_to_server{
        game_server.add_player(username, self);
    }
}

Sequence SendWaitingMsg(Text message) {
    OmidPlayer.doNothing{}
    OmidPlayerHelper.sendServer{
        game_server.broadcastWaitingMessage(message);
    }        
}

Sequence AddToGame() {
    OmidPlayer.doNothing{
    }
    OmidPlayerHelper.addPlayer {
        game_server.add_player(username);
    }
}

Sequence JoinWaiting() {
    OmidPlayer.doNothing {}
    OmidPlayerHelper.addToWaiting {
        game_server.add_to_waiting(username, self);
    }
}

Sequence LeaveWaiting() {
    OmidPlayer.doNothing{}
    OmidPlayerHelper.removePlayer {
        game_server.remove_from_waiting(username);
    }
}

Sequence CheckGameInSession() returns TrueFalse game_in_sess {
    OmidPlayer.doNothing{}
    OmidPlayerHelper.checkServer{
        game_in_sess = game_server.check_game_in_session();
    }
}

Sequence DisplayWaiting(Text message) {
    OmidPlayerHelper.doNothing{}
    OmidPlayer.displayWaitingMessage{
        extCopy message to waiting_message;
    }
}

Sequence UpdateGame() {
    Map(from: Text, to: Struct node_information) node_map;
    List(element: Struct arc_information) arc_map;
    Number score;
    OmidPlayerHelper.setMap{
        node_map = nodes;
        arc_map = arcs;
        score = game_server.get_score(username);
    }
    OmidPlayer.updateMap{
        clear_map();
        signal(update_score, score);
        for (Text answer in node_map) {
            node_list.append(node_map[answer]);
        }
        for (Struct arc_information arc_info in arc_map) {
            Struct node_information node1 = node_map[arc_info.node1];
            Struct node_information node2 = node_map[arc_info.node2];
            if (node1.found and node2.found) {
                draw_arc.append(node1.x);
                draw_arc.append(node1.y);
                draw_arc.append(node2.x);
                draw_arc.append(node2.y);
            }
        }
        print("finished updating");
    }
}

OmidPlayer {
    External Text waiting_message;
    External List(element: Struct node_information) node_list;
    External List(element: Number) draw_arc;
    Function (in: Text; returns: Nothing) update_score;
    Function(returns: Nothing) clear_map;
    onCreate(Text name, External Text gui, External List(element: Struct node_information) nodes, External List(element: Number) arc, Function(in: Text; returns: Nothing) score, Function(returns: Nothing) clear) {
        username = name;
        extAssign gui to waiting_message;
        extAssign nodes to node_list;
        extAssign arc to draw_arc;
        update_score = score;
        clear_map = clear;
        JoinWaiting();
    }
    Public Function add_to_game() {
        AddToGame();
    }

    Public Function game_in_session() returns TrueFalse {
        return CheckGameInSession();
    }
    /* Public Function game_ended() returns TrueFalse {
        return CheckGameOver();
        }*/
    Public Function send_to_waiting(Text message) {
        SendWaitingMsg(message);
    }

    Public Function send_answer(Text node_num, Text answer) {
        print("about to send a: " + node_num + " answer: " + answer);
        SendAnswer(node_num, answer);
        print("answer sent");
    }
}

OmidPlayerHelper {
    Endpoint game_server;
    Map(from: Text, to: Struct node_information) nodes;
    List(element: Struct arc_information) arcs;
    onCreate(Endpoint server) {
        game_server = server;
    }

    Public Function init_map(Map(from: Text, to: Struct node_information) node_info, List(element: Struct arc_information) arc_info) {
        nodes = node_info;
        arcs = arc_info;
        UpdateGame();
    }
    Public Function update_map(Text answer_found) {
        nodes[answer_found].found = True;
        UpdateGame();
    }

    Public Function get_new_waiting_message(Text message) {
        DisplayWaiting(message);
    }
}
